apply plugin: 'com.android.application'

ext.supportLibraryVersion = "25.3.1"

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "io.homeassistant.android"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 3
        versionName "1.0-RC1"
        setProperty("archivesBaseName", "hass-$versionName")

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        main
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-conf.txt'
            //noinspection GroovyAssignabilityCheck
            signingConfig signingConfigs.main
        }
        debug {
            debuggable true
            versionNameSuffix "-debug"
            //noinspection GroovyAssignabilityCheck
            signingConfig signingConfigs.main
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
    }
}

def propFile = project.file("keyStore.properties")
if (propFile.exists()) {
    def props = new Properties()
    props.load(new FileInputStream(propFile))
    android.signingConfigs.main.storeFile project.file(props.keyStore)
    android.signingConfigs.main.storePassword props.keyStorePassword
    android.signingConfigs.main.keyAlias props.keyAlias
    android.signingConfigs.main.keyPassword props.keyPassword
}

repositories {
    jcenter()
}

dependencies {
    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile "com.android.support:customtabs:$supportLibraryVersion"
    compile "com.android.support:design:$supportLibraryVersion"
    compile "com.android.support:preference-v14:$supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$supportLibraryVersion"

    compile 'com.squareup.okhttp3:okhttp:3.6.0'
    compile 'com.afollestad:ason:1.4.13'

    compile "com.google.android.gms:play-services-location:10.2.1"

    testCompile 'junit:junit:4.12'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2') {
        exclude group: 'com.android.support'
    }
    androidTestCompile('com.android.support.test:runner:0.5') {
        exclude group: 'com.android.support'
    }
    androidTestCompile('com.android.support.test:rules:0.5') {
        exclude group: 'com.android.support'
    }
}